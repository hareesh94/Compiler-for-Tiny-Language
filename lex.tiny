%{
#include <stdio.h>
#include <string.h>
#include <assert.h>
#include <Tokenizer.h>
#include "y.tab.h"

static int line = 1;
static int column = 1;
int rule(int token);
int node(int token);
void yyerror(char* message);
int debug_tokenizer = 0;

%}
%x COMM1
COMM2   #.*\n
IDENT	[_a-zA-Z][_a-zA-Z0-9]*
INT	[0-9]+
CHR     \'[^\']\'
WHITE   [ \t\v\f]*
LIT     '[^\']+'
STR     \"[^\"]*\"
%%

{WHITE}    	{ column += yyleng; }
\n         	{ column = 1; line++; }
"program"    	{ return rule(PROGRAM); }
"var" 		{ return rule(VAR); }
"const"         { return rule(CONST); }
"type"          { return rule(TYPE); }
"begin" 	{ return rule(BEGINX); }
"="		{ return rule(EQ); }
"end" 		{ return rule(END); }
":=" 		{ return rule(ASSIGNMENT); }
":=:"           { return rule(SWAP); }
"output" 	{ return rule(OUTPUT); }
"if" 		{ return rule(IF); }
"then" 		{ return rule(THEN); }
"else" 		{ return rule(ELSE); }
"while" 	{ return rule(WHILE); }
"do" 		{ return rule(DO); }
"succ"          { return rule(SUCC); }
"pred"          { return rule(PRED); }
"ord"           { return rule(ORD); }
"chr"           { return rule(CHR); }
"<=" 		{ return rule(LTE); }
"<>"		{ return rule(NE); }
">="		{ return rule(GTE); }
">"		{ return rule(GT); }
"<"		{ return rule(LT); }
"\*\*"          { return rule(EXP); }
"\.\."          { return rule(RANGE); }
"read" 		{ return rule(READ); }
"eof"		{ return rule(EOFX); }
"or"		{ return rule(OR); }
"mod"		{ return rule(MOD); }
"and"		{ return rule(AND); }
"not"		{ return rule(NOT); } 
"repeat"	{ return rule(REPEAT); }
"loop"          { return rule(LOOP); }
"pool"          { return rule(POOL); }
"exit"          { return rule(EXITX); }
"for"           { return rule(FOR); }
"to"            { return rule(UPTO); }
"downto"        { return rule(DOWNTO); }
"until"         { return rule(UNTIL); } 
"case"          { return rule(CASE); }
"of"            { return rule(OF); }
"otherwise"     { return rule(OTHERWISE); }
"function"      { return rule(FUNCTION); }
"return"        { return rule(RETURN); }
"procedure"     { return rule(PROCEDURE); }
{INT}		{ return node(INTEGER_NUM); }
{CHR}           { return node(CHAR); }
{STR}           { return node(STRING); }
{IDENT}		{ return node(IDENTIFIER); }
"{"		{ column += yyleng; BEGIN( COMM1 ); }
<COMM1>[^}\n]+  { column += yyleng; }
<COMM1>"}"	{ column += yyleng; BEGIN( INITIAL ); }
<COMM1>\n	{ column = 1; line++; }
{COMM2}         { column = 1; line++; }
":"             { return rule(yytext[0]); }
";"             { return rule(yytext[0]); }
"."             { return rule(yytext[0]); }
","             { return rule(yytext[0]); }
"("             { return rule(yytext[0]); }
")"             { return rule(yytext[0]); }
"*"		{ return rule(yytext[0]); }
"/"		{ return rule(yytext[0]); }
"+"             { return rule(yytext[0]); }
"-"             { return rule(yytext[0]); }
.               { yyerror("unrecognized char");
                  printf("-->%s<--\n",yytext);
                  column++; }  
%%

int rule(int token)
{
    if (debug_tokenizer) {
	printf("string:%s, token: %d, line: %d, column: %d\n",
	       yytext,token,line,column);
    }

    yylval.info.line = line;
    yylval.info.column = column;
    column += yyleng;

    yylval.info.string = malloc(yyleng+1);
    assert(yylval.info.string);
    strcpy(yylval.info.string, yytext); 

    yylval.info.makenode = 0;

    return token;
}

int node(int token)
{
    int tok = rule(token);
    yylval.info.makenode = 1;
    return tok;
}

void yyerror(char* message)
{
    printf("****** %s @line %d, column %d\n",
	   message,line,column);
}
